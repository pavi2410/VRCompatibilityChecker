apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def keystoreProperties = loadProps("keystore.properties")
def admobProps = loadProps("admob.properties")

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId 'appinventor.ai_pavitragolchha.VR'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 14
        versionName '7.1'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            def admobAppId = admobProps['appId']
            manifestPlaceholders = [admobAppId: admobAppId]
            buildConfigField 'String', 'ADMOB_APP_ID', "\"$admobAppId\""
            resValue 'string', 'banner_ad_unit_id', admobProps['bannerAdUnitId']
        }
        debug {
            applicationIdSuffix '.test'
            versionNameSuffix '-test'

            manifestPlaceholders = [admobAppId: 'ca-app-pub-3940256099942544~3347511713']
            buildConfigField 'String', 'ADMOB_APP_ID', "\"ca-app-pub-3940256099942544~3347511713\""
            resValue 'string', 'banner_ad_unit_id', 'ca-app-pub-3940256099942544/6300978111'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha01'
    implementation 'com.google.android.gms:play-services-ads-lite:18.3.0'
}

def loadProps(filename) {
    def file = rootProject.file(filename)
    def props = new Properties()
    props.load(new FileInputStream(file))
    return props
}